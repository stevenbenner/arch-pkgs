pkgbase=system-config
pkgname=('system-config-base' 'system-config-desktop' 'system-config-gnome' 'system-config-xfce')
pkgver=3
pkgrel=1
pkgdesc='Arch Linux system configuration'
arch=('any')
url='https://github.com/stevenbenner/arch-pkgs'
license=('none')
source=(
	'etc-locale.conf'
	'sudo-wheel'
	'paccache.hook'
	'mirrorupgrade.hook'
)
sha256sums=(
	'033252d5100cbb0028d7c15e075f63e4eb5ff7f1a923cd5cb547bcfd6b024f5b'
	'9db7f67e68218e51ee549cba0949aae86b6c14a48ac40b851cf76aba951f70a7'
	'ac76047ee7b0c57b225503f5d75cbdd70566e357f306ea5864688dc668aeac0a'
	'76ac369fd16b74ed4c617fea03fd37d8b47fb181e5c165873dbbfa852ad3f867'
)

package_system-config-base() {
	pkgdesc='Base system configuration'
	install='system-config-base.install'

	# base packages
	depends=('base' 'linux' 'linux-firmware' 'lvm2' 'man-db' 'man-pages' 'pacman-contrib' 'reflector')

	# base-devel packages
	depends+=('autoconf' 'automake' 'bison' 'gcc' 'highlight' 'make' 'patch' 'pkgconf' 'texinfo' 'which')

	# system packages
	depends+=('iptables' 'logrotate' 'nfs-utils' 'sudo' 'zsh' 'zsh-syntax-highlighting')

	# universal software
	depends+=('colordiff' 'git' 'htop' 'less' 'nano' 'nmap' 'pkgstats' 'screenfetch' 'stow' 'tree' 'wget')

	# microcode
	optdepends=(
		'amd-ucode: AMD processor support'
		'intel-ucode: Intel processor support'
	)

	install -Dm0644 etc-locale.conf "$pkgdir/etc/locale.conf"
	install -Dm0600 sudo-wheel "$pkgdir/etc/sudoers.d/wheel"
	install -Dm0644 paccache.hook "$pkgdir/usr/share/libalpm/hooks/paccache.hook"
	install -Dm0644 mirrorupgrade.hook "$pkgdir/usr/share/libalpm/hooks/mirrorupgrade.hook"
}

package_system-config-desktop() {
	pkgdesc='Desktop system configuration'

	# standard software
	depends=('chromium' 'firefox' 'keepassxc' 'mpv' 'vlc')

	# fonts
	depends+=(
		'adobe-source-code-pro-fonts'
		'gnu-free-fonts'
		'otf-ipafont'
		'ttf-anonymous-pro'
		'ttf-bitstream-vera'
		'ttf-droid'
		'ttf-inconsolata'
		'ttf-liberation'
		'ttf-roboto'
	)

	# graphics drivers
	optdepends=(
		'mesa: AMD graphics support'
		'nvidia: Nvidia graphics support'
		'xf86-video-intel: Intel graphics support on Xorg'
	)
}

package_system-config-gnome() {
	pkgdesc='GNOME desktop environment configuration'
	install='system-config-gnome.install'

	# inherit common desktop config
	depends=('system-config-desktop')

	# supporting packages
	depends+=('networkmanager')

	# gnome group packages
	depends+=(
		'eog'
		'gdm'
		'gedit'
		'gnome-backgrounds'
		'gnome-calculator'
		'gnome-calendar'
		'gnome-characters'
		'gnome-clocks'
		'gnome-color-manager'
		'gnome-control-center'
		'gnome-disk-utility'
		'gnome-font-viewer'
		'gnome-keyring'
		'gnome-logs'
		'gnome-menus'
		'gnome-screenshot'
		'gnome-session'
		'gnome-settings-daemon'
		'gnome-shell'
		'gnome-shell-extensions'
		'gnome-system-monitor'
		'gnome-terminal'
		'gnome-themes-extra'
		'gnome-user-docs'
		'gnome-weather'
		'gvfs'
		'gvfs-nfs'
		'gvfs-smb'
		'mutter'
		'nautilus'
		'sushi'
		'xdg-user-dirs-gtk'
	)

	# gnome-extra group packages
	depends+=('gnome-tweaks' 'gnome-usage')
}

package_system-config-xfce() {
	pkgdesc='Xfce desktop environment configuration'
	install='system-config-xfce.install'

	# inherit common desktop config
	depends=('system-config-desktop')

	# supporting packages
	depends+=('light-locker' 'lightdm' 'lightdm-gtk-greeter' 'xcape' 'xorg-xinput')

	# xfce group packages
	depends+=(
		'thunar'
		'thunar-volman'
		'xfce4-appfinder'
		'xfce4-panel'
		'xfce4-power-manager'
		'xfce4-session'
		'xfce4-settings'
		'xfce4-terminal'
		'xfdesktop'
		'xfwm4'
		'xfwm4-themes'
	)

	# xfce-goodies group packages
	depends+=(
		'mousepad'
		'ristretto'
		'thunar-archive-plugin'
		'thunar-media-tags-plugin'
		'xfburn'
		'xfce4-artwork'
		'xfce4-clipman-plugin'
		'xfce4-cpufreq-plugin'
		'xfce4-cpugraph-plugin'
		'xfce4-datetime-plugin'
		'xfce4-diskperf-plugin'
		'xfce4-fsguard-plugin'
		'xfce4-mount-plugin'
		'xfce4-netload-plugin'
		'xfce4-notes-plugin'
		'xfce4-notifyd'
		'xfce4-pulseaudio-plugin'
		'xfce4-screensaver'
		'xfce4-screenshooter'
		'xfce4-sensors-plugin'
		'xfce4-systemload-plugin'
		'xfce4-taskmanager'
		'xfce4-whiskermenu-plugin'
	)
}
