# Maintainer: Steven Benner

pkgbase=system-config
pkgname=(
	'system-config-base'
	'system-config-desktop'
	'system-config-devel'
	'system-config-gnome'
	'system-config-nvidia'
	'system-config-workstation'
	'system-config-xfce'
)
pkgver=33
pkgrel=1
pkgdesc='Arch Linux system configuration'
arch=('any')
url='https://github.com/stevenbenner/arch-pkgs'
license=('none')
source=(
	'etc-locale.conf'
	'sudo-wheel'
	'ssh_known_hosts'
	'paccache.hook'
	'mirrorupgrade.hook'
	'modprobe-nvidia.conf'
	'network-20-ethernet.network'
	'xorg-20-nvidia.conf'
	'xorg-50-mouse-acceleration.conf'
)
sha256sums=(
	'033252d5100cbb0028d7c15e075f63e4eb5ff7f1a923cd5cb547bcfd6b024f5b'
	'9db7f67e68218e51ee549cba0949aae86b6c14a48ac40b851cf76aba951f70a7'
	'7365424a09fde1acd6f8e5c17399a9fa22cb87f791bddc62ffe9e52d2d2dbf16'
	'ac76047ee7b0c57b225503f5d75cbdd70566e357f306ea5864688dc668aeac0a'
	'76ac369fd16b74ed4c617fea03fd37d8b47fb181e5c165873dbbfa852ad3f867'
	'ece12744f3ca404c60bfa9de9067d8916e3de69bba2074ee30bcbaca53bde08e'
	'c570adbee3abd47639d57ce1cf669525d50b89ac4d1885177c86ffc26bba0cbe'
	'10d5a050d0470a10f2b4a962082e399f0db10370f08dbf2ef949612ba37ce7fc'
	'03d53ef1269aaa67efcadfeea1ffcb75f4eeca4a045b65d707c7a1ea21014b6a'
)

package_system-config-base() {
	pkgdesc='Base system configuration'
	install='system-config-base.install'

	# base packages
	depends=(
		'base'                      # arch linux base metapackage
		'base-devel'                # software build dependencies meta package
		'linux'                     # standard kernel
		'linux-firmware'            # hardware firmware/drivers
		'lvm2'                      # lvm support
		'man-db'                    # man support
		'man-pages'                 # man support
		'pacman-contrib'            # pacman utilities, including paccache
		'reflector'                 # automatic arch mirror configuration tool
	)

	# system packages
	depends+=(
		'iptables'                  # firewall
		'logrotate'                 # log file manager
		'nfs-utils'                 # nfs support
		'smartmontools'             # storage device s.m.a.r.t. tools
		'zsh'                       # zsh
		'zsh-syntax-highlighting'   # console syntax highlighting
	)

	# universal software
	depends+=(
		'colordiff'                 # diff command with syntax highlighting, used by dotfiles
		'fastfetch'                 # system information utility
		'git'                       # version control software, used for dotfiles and aur
		'htop'                      # improved version of top
		'iftop'                     # network interface usage tool
		'less'                      # cli text viewer
		'lostfiles'                 # tool to find system files not being managed by pacman
		'nano'                      # cli text editor
		'nano-syntax-highlighting'  # extra nano syntax highlighting, used by dotfiles
		'nethogs'                   # network interface usage tool
		'nmap'                      # network diagnostics tool
		'pkgstats'                  # send installed packages and basic system info for statistics
		'stow'                      # symlink utility, used for dotfiles
		'traceroute'                # network diagnostics tool
		'tree'                      # directory list tool
		'unrar'                     # extract rar files
		'unzip'                     # extract zip files
		'usbutils'                  # lsusb command
		'wget'                      # cli download utility
		'zip'                       # create zip files
	)

	# microcode
	optdepends=(
		'amd-ucode: AMD processor support'
		'intel-ucode: Intel processor support'
	)

	# file systems
	optdepends+=(
		'btrfs-progs: Btrfs support'
	)

	install -Dm0644 etc-locale.conf "$pkgdir/etc/locale.conf"
	install -Dm0440 sudo-wheel "$pkgdir/etc/sudoers.d/wheel"
	install -Dm0644 ssh_known_hosts "$pkgdir/etc/ssh/ssh_known_hosts"
	install -Dm0644 paccache.hook "$pkgdir/usr/share/libalpm/hooks/paccache.hook"
	install -Dm0644 mirrorupgrade.hook "$pkgdir/usr/share/libalpm/hooks/mirrorupgrade.hook"

	chmod 750 "$pkgdir/etc/sudoers.d/"
}

package_system-config-desktop() {
	pkgdesc='Desktop system configuration'

	# inherit base config
	depends=('system-config-base')

	# standard software
	depends+=(
		'chromium'                  # web browser
		'firefox'                   # web browser
		'hunspell-en_us'            # firefox spell checking
		'keepassxc'                 # password manager
		'mpv'                       # video playback software
		'nvtop'                     # a top-like tool for graphics processors
		'qalculate-gtk'             # advanced calculator
		'vlc'                       # video playback software
		'yt-dlp'                    # cli video downloader, support youtube (and others) in mpv
	)

	# system packages
	depends+=(
		'pipewire-pulse'            # system sound server
	)

	# fonts
	depends+=(
		'adobe-source-code-pro-fonts'  # monospace font designed for source code
		'gnu-free-fonts'            # commonly used collection of free fonts
		'noto-fonts-emoji'          # emoji font for chromium
		'otf-ipafont'               # japanese language font
		'ttf-anonymous-pro'         # monospace font designed for source code
		'ttf-bitstream-vera'        # commonly used collection of fonts
		'ttf-droid'                 # android fonts
		'ttf-inconsolata'           # monospace font designed for code and terminal
		'ttf-liberation'            # free fonts similar to older windows fonts
		'ttf-roboto'                # google fonts
	)

	# graphics drivers
	optdepends=(
		'mesa: AMD graphics support'
		'system-config-nvidia: Nvidia graphics support'
		'xf86-video-intel: Intel graphics support on Xorg'
	)
}

package_system-config-devel() {
	pkgdesc='Development system configuration'
	install='system-config-devel.install'

	# inherit common desktop config
	depends=('system-config-desktop')

	# dev software
	depends+=(
		'aurpublish'                # arch user repository git project management tool
		'filezilla'                 # ftp/ftps/sftp client
		'gdb'                       # debugging tool
		'git-zsh-completion'        # official zsh completions for git
		'grunt-cli'                 # javascript task runner
		'meld'                      # visual diff and merge tool
		'namcap'                    # pacman package analyzer
		'npm'                       # node.js package manager
		'rsync'                     # remote file copying tool
		'rust'                      # rust language compiler/utilities
		'shellcheck'                # shell script analysis tool
		'strace'                    # system trace/diagnostics tool
		'strip-nondeterminism'      # deterministic archive tool
		'tokei'                     # source code line counter and report tool
		'valgrind'                  # memory leak finder
	)
}

package_system-config-gnome() {
	pkgdesc='GNOME desktop environment configuration'
	install='system-config-gnome.install'

	# inherit common desktop config
	depends=('system-config-desktop')

	# supporting packages
	depends+=(
		'gnome-browser-connector'   # chromium support for extensions.gnome.org
		'hunspell'                  # spell checking backend for enchant
		'networkmanager'            # networking config/manager service
		'power-profiles-daemon'     # power profile support
	)

	# gnome group packages
	depends+=(
		'baobab'                    # directory tree analyzer
		'eog'                       # image viewer
		'gdm'                       # gnome display manager
		'gedit'                     # text editor
		'gnome-backgrounds'         # small collection of background images
		'gnome-calculator'          # calculator
		'gnome-characters'          # character map viewer
		'gnome-clocks'              # clock
		'gnome-color-manager'       # color profile utility
		'gnome-control-center'      # gnome settings tool
		'gnome-disk-utility'        # drive/partition viewer
		'gnome-font-viewer'         # font viewer
		'gnome-keyring'             # key ring
		'gnome-logs'                # log viewer
		'gnome-menus'               # gnome's menu system
		'gnome-screenshot'          # screenshot tool
		'gnome-session'             # session manager
		'gnome-settings-daemon'     # settings back-end
		'gnome-shell'               # gnome desktop functionality
		'gnome-system-monitor'      # system monitor utility
		'gnome-terminal'            # terminal
		'gnome-themes-extra'        # gnome themes
		'gnome-user-docs'           # gnome documentation
		'gvfs'                      # virtual filesystem tool
		'gvfs-nfs'                  # nfs support for gvfs
		'mutter'                    # window manager
		'nautilus'                  # file browser
		'sushi'                     # nautilus file previewer
		'xdg-desktop-portal-gnome'  # xdg-desktop-portal for gnome
		'xdg-user-dirs-gtk'         # creates xdg user directories
	)

	# gnome-extra group packages
	depends+=(
		'file-roller'               # file archive tool
		'gnome-tweaks'              # extra settings manager
		'gnome-usage'               # system monitor utility
	)

	# prevent enabling multiple display managers
	conflicts=('system-config-xfce')
}

package_system-config-nvidia() {
	pkgdesc='Nvidia graphics system configuration'

	# inherit base config
	depends=('system-config-base')

	# packages
	depends+=(
		'nvidia'                    # nvidia drivers
		'nvidia-settings'           # nvidia configuration utility
	)

	install -Dm0644 modprobe-nvidia.conf "$pkgdir/etc/modprobe.d/nvidia.conf"
	install -Dm0644 xorg-20-nvidia.conf "$pkgdir/etc/X11/xorg.conf.d/20-nvidia.conf"
}

package_system-config-workstation() {
	pkgdesc='Workstation system configuration'

	# inherit common desktop config
	depends=('system-config-desktop')

	# software
	depends+=(
		'aspell'                    # pidgin spell checking
		'gimp'                      # image editor
		'hexchat'                   # irc client
		'inkscape'                  # vector graphics editor
		'libreoffice-fresh'         # libreoffice
		'openvpn'                   # vpn client
		'pidgin'                    # instant messaging client
		'pidgin-otr'                # otr messaging plugin for pidgin
		'thunderbird'               # email and rss client
		'virtualbox'                # virtual machines
		'virtualbox-guest-iso'      # guest drivers for virtualbox
	)
}

package_system-config-xfce() {
	pkgdesc='Xfce desktop environment configuration'
	install='system-config-xfce.install'

	# inherit common desktop config
	depends=('system-config-desktop')

	# supporting packages
	depends+=(
		'light-locker'              # session locker for lightdm
		'lightdm'                   # display manager
		'lightdm-gtk-greeter'       # default greeter (login screen) for lightdm
		'pavucontrol'               # dependency of xfce4-pulseaudio-plugin
		'redshift'                  # nighttime screen color temperature utility
		'xcape'                     # key mapping tool, used for super key support in dotfiles
		'xorg-xinput'               # input device config tool, used to set mouse acceleration
	)

	# xfce group packages
	depends+=(
		'thunar'                    # file browser
		'thunar-volman'             # mounting utility for thunar
		'xfce4-appfinder'           # application finder utility
		'xfce4-panel'               # panel support
		'xfce4-power-manager'       # power settings tool
		'xfce4-session'             # session manager
		'xfce4-settings'            # settings manager
		'xfce4-terminal'            # terminal
		'xfdesktop'                 # desktop manager
		'xfwm4'                     # window manager
		'xfwm4-themes'              # default themes
	)

	# xfce-goodies group packages
	depends+=(
		'mousepad'                  # text editor
		'ristretto'                 # image viewer
		'thunar-archive-plugin'     # zip/tar/etc support for thunar
		'thunar-media-tags-plugin'  # media tags support for thunar
		'xfce4-artwork'             # backgrounds
		'xfce4-cpufreq-plugin'      # cpu status widget
		'xfce4-cpugraph-plugin'     # cpu status widget
		'xfce4-diskperf-plugin'     # hdd status widget
		'xfce4-netload-plugin'      # network status widget
		'xfce4-notifyd'             # notification support
		'xfce4-pulseaudio-plugin'   # pulse audio widget
		'xfce4-screensaver'         # screensaver
		'xfce4-screenshooter'       # screenshot tool
		'xfce4-sensors-plugin'      # sensors widget
		'xfce4-systemload-plugin'   # system monitor widget
		'xfce4-taskmanager'         # task manager
		'xfce4-whiskermenu-plugin'  # enhanced "start" menu
	)

	# useful software packages
	depends+=(
		'mate-calc'                 # simple calculator
		'xarchiver'                 # file archive tool
	)

	# prevent enabling multiple display managers
	conflicts=('system-config-gnome')

	install -Dm0644 network-20-ethernet.network "$pkgdir/etc/systemd/network/20-ethernet.network"
	install -Dm0644 xorg-50-mouse-acceleration.conf "$pkgdir/etc/X11/xorg.conf.d/50-mouse-acceleration.conf"
}
